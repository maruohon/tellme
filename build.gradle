plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
	minecraft "com.mojang:minecraft:${config.minecraft_version}"
	mappings "net.fabricmc:yarn:${config.mappings_version}:v2"
	modImplementation "net.fabricmc:fabric-loader:${config.fabric_loader_version}"
	implementation "com.google.code.findbugs:jsr305:3.0.2"

	modImplementation(fabricApi.module("fabric-command-api-v2", config.fabric_api_version))
	include(modImplementation(fabricApi.module("fabric-command-api-v2", config.fabric_api_version)))

	// fabric-registry-sync-v0 is required for the command argument registration to
	// not crash the game due to the registry being frozen already.
	// And fabric-networking-api-v1 and fabric-api-base seem to be
	// requirements for fabric-registry-sync-v0 and/or fabric-command-api-v2
	include(modImplementation(fabricApi.module("fabric-registry-sync-v0", config.fabric_api_version)))
	include(modImplementation(fabricApi.module("fabric-networking-api-v1", config.fabric_api_version)))
	include(modImplementation(fabricApi.module("fabric-api-base", config.fabric_api_version)))

	// Fabric API. This is technically optional, but you probably want it anyway.
	//modCompile "net.fabricmc.fabric-api:fabric-api:" + config.fabric_version
}

group = config.group + "." + config.mod_id
archivesBaseName = config.mod_file_name + '-' + config.minecraft_version_out
version = config.mod_version

if (version.endsWith('-dev')) {
	version += "." + new Date().format('yyyyMMdd.HHmmss')
}

processResources {
	// Exclude the GIMP image files
	exclude '**/*.xcf'
	exclude '**/xcf'

	// this will ensure that this task is redone when the versions change.
	//inputs.property "minecraft_version", project.config.minecraft_version

	inputs.property "mod_version", config.mod_version

	filesMatching("fabric.mod.json") {
		expand "mod_version": config.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}
